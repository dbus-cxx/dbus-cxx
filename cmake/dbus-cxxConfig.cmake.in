@PACKAGE_INIT@

set(dbus-cxx_KNOWN_COMPONENTS "glib" "qt" "uv")

include(CMakeFindDependencyMacro)

find_dependency(Threads)
find_dependency(PkgConfig)
pkg_check_modules(sigc REQUIRED IMPORTED_TARGET sigc++-3.0)

if(@ENABLE_GLIB_SUPPORT@)
    pkg_check_modules(glib REQUIRED IMPORTED_TARGET glib-2.0)
endif()

if(@ENABLE_UV_SUPPORT@)
    if(@UV_STATIC@)
        pkg_check_modules(libuv REQUIRED IMPORTED_TARGET libuv-static)
    else()
        pkg_check_modules(libuv REQUIRED IMPORTED_TARGET libuv)
    endif()
endif()

if(@ENABLE_QT_SUPPORT@)
    if(@QT_VERSION_MAJOR@ EQUAL 6)
        find_dependency(Qt6 COMPONENTS Core)
    else()
        find_dependency(Qt5 COMPONENTS Core)
    endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/dbus-cxxTargets.cmake")

if("glib" IN_LIST dbus-cxx_FIND_COMPONENTS)
    if(@ENABLE_GLIB_SUPPORT@)
        if(TARGET dbus-cxx::dbus-cxx-glib)
            set(dbus-cxx_glib_FOUND TRUE)
        else()
            set(dbus-cxx_glib_FOUND FALSE)
            if(dbus-cxx_FIND_REQUIRED_glib)
                message(FATAL_ERROR "Requested 'glib' component but target 'dbus-cxx::dbus-cxx-glib' is not available.")
            endif()
        endif()
    else()
        set(dbus-cxx_glib_FOUND FALSE)
        if(dbus-cxx_FIND_REQUIRED_glib)
            message(FATAL_ERROR "Requested 'glib' component but dbus-cxx was built without glib support.")
        endif()
    endif()
endif()

if("uv" IN_LIST dbus-cxx_FIND_COMPONENTS)
    if(@ENABLE_UV_SUPPORT@)
        if(TARGET dbus-cxx::dbus-cxx-uv)
            set(dbus-cxx_uv_FOUND TRUE)
        else()
            set(dbus-cxx_uv_FOUND FALSE)
            if(dbus-cxx_FIND_REQUIRED_uv)
                message(FATAL_ERROR "Requested 'uv' component but target 'dbus-cxx::dbus-cxx-uv' is not available.")
            endif()
        endif()
    else()
        set(dbus-cxx_uv_FOUND FALSE)
        if(dbus-cxx_FIND_REQUIRED_uv)
            message(FATAL_ERROR "Requested 'uv' component but dbus-cxx was built without uv support.")
        endif()
    endif()
endif()

if("qt" IN_LIST dbus-cxx_FIND_COMPONENTS)
    if(@ENABLE_QT_SUPPORT@)
        if(TARGET dbus-cxx::dbus-cxx-qt)
            set(dbus-cxx_qt_FOUND TRUE)
        else()
            set(dbus-cxx_qt_FOUND FALSE)
            if(dbus-cxx_FIND_REQUIRED_qt)
                message(FATAL_ERROR "Requested 'qt' component but target 'dbus-cxx::dbus-cxx-qt' is not available.")
            endif()
        endif()
    else()
        set(dbus-cxx_qt_FOUND FALSE)
        if(dbus-cxx_FIND_REQUIRED_qt)
            message(FATAL_ERROR "Requested 'qt' component but dbus-cxx was built without qt support.")
        endif()
    endif()
endif()
